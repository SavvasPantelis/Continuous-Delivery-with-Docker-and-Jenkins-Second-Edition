buildscript {
    dependencies {
        classpath('com.h2database:h2:1.4.199')
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java'
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: "jacoco"
apply plugin: 'checkstyle'

group = 'com.leszko'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.hazelcast:hazelcast-all:3.12'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("io.cucumber:cucumber-java:4.2.6")
    testImplementation("io.cucumber:cucumber-junit:4.2.6")
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.2
            }
        }
    }
}

checkstyle {
    checkstyleTest.enabled = false
}

task acceptanceTest(type: Test) {
    include '**/acceptance/**'
    systemProperties System.getProperties()
    jacoco.enabled = false // Disable Jacoco for this task

    // Configure the test reports to generate in JUnit format
    reports {
        junitXml.enabled = true
        html.enabled = false
        junitXml.destination = file('build/reports/tests/acceptanceTest') // Output path for XML reports
    }
}

task smokeTest(type: Test) {
    include '**/acceptance/**'
    systemProperties System.getProperties()
}

test {
    jacoco.enabled = false // Disable Jacoco for regular tests
    exclude '**/acceptance/**'
    exclude '**/smoke/**'
}

flyway {
    url = 'jdbc:h2:file:/tmp/calculator'
    user = 'sa'
}

